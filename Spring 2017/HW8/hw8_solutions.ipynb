{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# CS/ECE/ISyE 524 - Spring 2017 - Homework 8 - Solutions\n",
    "### Prepared by: Laurent Lessard"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1. Voting\n",
    "\n",
    "Governor Blue of the state of Berry is attempting to get the state\n",
    "legislator to gerrymander Berry's congressional districts.  The state\n",
    "consists of ten cities, and the numbers of registered Republicans and\n",
    "Democrats (in thousands) in each city are shown below\n",
    "\n",
    "| City | Republicans | Democrats |\n",
    "|:----:|:-----------:|:---------:|\n",
    "|1     |  80         |    34     |\n",
    "|2     |  60         |    44     |\n",
    "|3     |  40         |    44     |\n",
    "|4     |  20         |    24     |\n",
    "|5     |  40         |    114    |\n",
    "|6     |  40         |    64     |\n",
    "|7     |  70         |    14     |\n",
    "|8     |  50         |    44     |\n",
    "|9     |  70         |    54     |\n",
    "|10    |  70         |    64     |\n",
    "\n",
    "Berry has five congressional representatives.  To form the five congressional\n",
    "districts, cities must be grouped together according to the following\n",
    "restrictions:\n",
    "\n",
    "- Districts cannot subdivide cities; all voters in a city must be in the same district.\n",
    "- Each district must contain between 150,000 and 250,000 voters (there are no independent voters).  \n",
    "\n",
    "Governor Blue is a Democrat. Assume 100\\% voter turnout and that each voter always votes according to their registered party.  Formulate and solve an optimization problem to help Governor Blue maximize the number of congressional districts that have a Democratic majority."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "The assignment of cities to districts is:\n",
      "5×10 Named Array{Float64,2}\n",
      "A ╲ B │   1    2    3    4    5    6    7    8    9   10\n",
      "──────┼─────────────────────────────────────────────────\n",
      "A     │ 0.0  0.0  1.0  1.0  0.0  0.0  0.0  1.0  0.0  0.0\n",
      "B     │ 1.0  0.0  0.0  0.0  0.0  0.0  0.0  0.0  1.0  0.0\n",
      "C     │ 0.0  1.0  0.0  0.0  0.0  1.0  0.0  0.0  0.0  0.0\n",
      "D     │ 0.0  0.0  0.0  0.0  1.0  0.0  0.0  0.0  0.0  0.0\n",
      "E     │ 0.0  0.0  0.0  0.0  0.0  0.0  1.0  0.0  0.0  1.0\n",
      "The final tally of republicans and democrats in each district is:\n",
      "5×2 Named Array{Float64,2}\n",
      "A ╲ B │   rep    dem\n",
      "──────┼─────────────\n",
      "A     │ 110.0  112.0\n",
      "B     │ 150.0   88.0\n",
      "C     │ 100.0  108.0\n",
      "D     │  40.0  114.0\n",
      "E     │ 140.0   78.0\n",
      "The democrats win a total of 3 districts (out of 5)\n"
     ]
    }
   ],
   "source": [
    "using JuMP, NamedArrays\n",
    "\n",
    "parties = [:rep, :dem]\n",
    "districts = [:A, :B, :C, :D, :E];  # five districts\n",
    "cities = collect(1:10)\n",
    "\n",
    "# first column is republican, second column is democrat\n",
    "# columns are cities\n",
    "data = [ 80 34\n",
    "60 44\n",
    "40 44\n",
    "20 24\n",
    "40 114\n",
    "40 64\n",
    "70 14\n",
    "50 44\n",
    "70 54\n",
    "70 64 ];\n",
    "\n",
    "counts = NamedArray( data, (cities,parties))\n",
    "\n",
    "\n",
    "m = Model()\n",
    "\n",
    "# does district i contain city j?\n",
    "@variable(m, x[districts,cities], Bin)\n",
    "\n",
    "# do the democrats win this district?\n",
    "@variable(m, z[districts], Bin)\n",
    "\n",
    "# each district must contain between 150k and 250k total voters\n",
    "for i in districts\n",
    "    @constraint(m, 150 <= sum( (counts[j,:rep] + counts[j,:dem])*x[i,j] for j in cities ) <= 250 )\n",
    "end\n",
    "\n",
    "# each city belongs to exactly one district\n",
    "for j in cities\n",
    "    @constraint(m, sum( x[i,j] for i in districts) == 1)\n",
    "end\n",
    "\n",
    "# since there are at most 250k per district, there are at most 250k republicans.\n",
    "# since there are at least 150k per district, there are at least 150 democrats.\n",
    "# therefore, #reps - #dems <= 250 - 150 = 100.\n",
    "# if z=1, there must be more democrats than republicans in the district.\n",
    "for i in districts\n",
    "    @constraint(m, sum( (counts[j,:rep] - counts[j,:dem])*x[i,j] for j in cities) <= 100*(1-z[i]) )\n",
    "end\n",
    "\n",
    "@objective(m, Max, sum(z))\n",
    "\n",
    "solve(m)\n",
    "\n",
    "sol = NamedArray( zeros(5,10), (districts,cities))\n",
    "for i in districts\n",
    "    for j in cities\n",
    "        sol[i,j] = getvalue(x[i,j])\n",
    "    end\n",
    "end\n",
    "\n",
    "println(\"The assignment of cities to districts is:\")\n",
    "show(sol)\n",
    "println()\n",
    "\n",
    "println(\"The final tally of republicans and democrats in each district is:\")\n",
    "show(sol*counts)\n",
    "\n",
    "println()\n",
    "println(\"The democrats win a total of \", Int(getobjectivevalue(m)), \" districts (out of 5)\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 2. Paint production\n",
    "\n",
    "As part of its weekly production, a paint company produces five batches of paints, always the same, for some big clients who have a stable demand.  Every paint batch is produced in a single production process, all in the same blender that needs to be cleaned between each batch.  The durations of blending paint batches 1 to 5 are 40, 35, 45, 32 and 50 minutes respectively.  The cleaning times depend of the colors and the paint types.  For example, a long cleaning period is required if an oil-based paint is produced after a water-based paint, or to produce white paint after a dark color.  The times are given in minutes in the following matrix $A$ where $A_{ij}$ denotes the cleaning time after batch $i$ if it is followed by batch $j$.\n",
    "\n",
    "$$\n",
    "A = \\begin{bmatrix}\n",
    " 0&11& 7&13&11 \\\\\n",
    " 5& 0&13&15&15 \\\\\n",
    "13&15& 0&23&11 \\\\\n",
    " 9&13& 5& 0& 3 \\\\\n",
    " 3& 7& 7& 7& 0\n",
    "\\end{bmatrix}\n",
    "$$\n",
    "\n",
    "Since the company has other activities, it wishes to deal with this weekly production in the shortest possible time (blending and cleaning).  What is the corresponding order of paint batches?  The order will be applied every week, so the cleaning time between the last batch of one week and the first of the following week needs to be accounted for in the total duration of cleaning."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Optimal ordering is:\n",
      "[4]\n",
      "[1]\n",
      "[5]\n",
      "[3]\n",
      "[2]\n",
      "and the minimum cleanup time is: 41.0 min, (plus 202 min of blending)\n"
     ]
    }
   ],
   "source": [
    "# This is a traveling salesman problem! cities correspond to paint batches, and the time it takes to travel\n",
    "# from one city to the next is the time it takes to clean the blender in between two particular batches.\n",
    "\n",
    "# Because the cleaning time between the last batch of one week and the first batch of the following week needs\n",
    "# to be accounted for, this is EXACTLY a traveling salesman problem!\n",
    "\n",
    "# A[i,j] is the time it takes to travel j --> i.\n",
    "A = [  0  11   7  13  11\n",
    "       5   0  13  15  15\n",
    "      13  15   0  23  11\n",
    "       9  13   5   0   3\n",
    "       3   7   7   7   0 ]\n",
    "\n",
    "# time it takes to blend a particular batch. This information isn't important, because we need to blend each batch\n",
    "# every week no matter what order they're blended in. So this is a fixed cost!\n",
    "b = [ 40, 35, 45, 32, 50 ]\n",
    "\n",
    "using JuMP\n",
    "m = Model()\n",
    "n = length(b)  # number of paints\n",
    "\n",
    "# x[i,j] = 1 if we blend i immediately after having blended j.\n",
    "@variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "# can't flow from a node to itself\n",
    "@constraint(m, nzdiag[i=1:n], x[i,i] == 0 )\n",
    "\n",
    "# flow constraints (exactly one flow out of each )\n",
    "@constraint(m,  inflows[i=1:n], sum( x[i,j] for j=1:n ) == 1 )\n",
    "@constraint(m, outflows[j=1:n], sum( x[i,j] for i=1:n ) == 1 )\n",
    "\n",
    "# additional variables and constraints for MTZ formulation\n",
    "@variable(m, u[1:n] >= 0)\n",
    "@constraint(m, MTZ[i=1:n, j=2:n], u[i] - u[j] + n*x[i,j] <= n-1)\n",
    "\n",
    "@objective(m, Min, sum( x[i,j]*A[i,j] for i=1:n, j=1:n ))\n",
    "\n",
    "solve(m)\n",
    "\n",
    "println(\"Optimal ordering is:\")\n",
    "            X = getvalue(x);\n",
    "            for i = 1:n\n",
    "                println(find(X[i,:]))\n",
    "            end\n",
    "#            println(getvalue(x))\n",
    "println(\"and the minimum cleanup time is: \", getobjectivevalue(m), \" min, (plus \", sum(b), \" min of blending)\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 3. The Queens problem\n",
    "You are given a standard 8 x 8 chess board. The following problems involve placing queens on the board such that certain constraints are satisfied. For each of the following problems, model the optimization task as an integer program, solve it, and show what an optimal placement of queens on the board looks like.\n",
    "\n",
    "**(a)** Find a way to place 8 queens on the board so that no two queens threaten each other. We say that two queens \\emph{threaten} each other if they occupy the same row, column, or diagonal. Show what this placement looks like.\n",
    "\t\n",
    "**(b)** Repeat part (a) but this time find a placement of the 8 queens that has point symmetry. In other words, find a placement that looks the same if you rotate the board 180&deg;.\n",
    "\t\n",
    "**(c)** What is the smallest number of queens that we can place on the board so that each empty cell is threatened by at least one queen? Show a possible optimal placement.\n",
    "\t\n",
    "**(d)** Repeat part (c) but this time find a placement of the queens that also has point symmetry. Does the minimum number of queens required change? Show a possible optimal placement."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "printBoard (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# helper function to print a chess board\n",
    "function printBoard(X,n)\n",
    "    print(\"+\")\n",
    "    for i in 1:n\n",
    "        print(\"---+\")\n",
    "    end\n",
    "    print(\"\\n\")\n",
    "    \n",
    "    for i in 1:n\n",
    "        print(\"| \")\n",
    "        for j in 1:n\n",
    "            X[i,j] == 1 ? print(\"X\") : print(\" \")\n",
    "            print(\" | \")\n",
    "        end\n",
    "        print(\"\\n\")\n",
    "        print(\"+\")\n",
    "        for j in 1:n\n",
    "            print(\"---+\")\n",
    "        end\n",
    "        print(\"\\n\")\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "A solution is: \n",
      "+---+---+---+---+---+---+\n",
      "|   |   |   |   | X |   | \n",
      "+---+---+---+---+---+---+\n",
      "|   |   | X |   |   |   | \n",
      "+---+---+---+---+---+---+\n",
      "| X |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+\n",
      "|   |   |   |   |   | X | \n",
      "+---+---+---+---+---+---+\n",
      "|   |   |   | X |   |   | \n",
      "+---+---+---+---+---+---+\n",
      "|   | X |   |   |   |   | \n",
      "+---+---+---+---+---+---+\n"
     ]
    }
   ],
   "source": [
    "# solve the 8 queens problem (queens don't threaten each other)\n",
    "\n",
    "using JuMP\n",
    "\n",
    "n = 6\n",
    "\n",
    "m = Model()\n",
    "\n",
    "@variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "for i in 1:n\n",
    "    @constraint(m, sum(x[i,j] for j in 1:n) == 1)  # exactly one per row\n",
    "    @constraint(m, sum(x[j,i] for j in 1:n) == 1)  # exactly one per column\n",
    "end\n",
    "for i in 1:n\n",
    "    @constraint(m, sum(x[i+k,1+k] for k in 0:n-i) <= 1)  # \"\\\" upper diagonal\n",
    "    @constraint(m, sum(x[i-k,1+k] for k in 0:i-1) <= 1)  # \"/\" upper diagonal\n",
    "end\n",
    "for j in 2:n\n",
    "    @constraint(m, sum(x[1+k,j+k] for k in 0:n-j) <= 1)  # \"\\\" lower diagonal\n",
    "    @constraint(m, sum(x[n-k,j+k] for k in 0:n-j) <= 1)  # \"/\" lower diagonal\n",
    "end\n",
    "\n",
    "# SYMMETRY CONSTRAINT --- REMOVE THIS FOR NON-SYMMETRIC SOLUTIONS\n",
    "for i in 1:n\n",
    "    for j in i:n\n",
    "        @constraint(m, x[i,j] == x[n-i+1,n-j+1] )\n",
    "    end\n",
    "end\n",
    "\n",
    "status = solve(m)\n",
    "X = getvalue(x)\n",
    "println(\"A solution is: \")\n",
    "printBoard(X,n)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "A solution is: \n",
      "+---+---+---+---+---+---+---+---+\n",
      "| X |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   | X |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   | X |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   | X | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   | X |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n"
     ]
    }
   ],
   "source": [
    "# solve the min queen problem (all squares are threatened)\n",
    "\n",
    "using JuMP\n",
    "\n",
    "n = 8\n",
    "\n",
    "m = Model()\n",
    "\n",
    "@variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "for i = 1:n\n",
    "    for j = 1:n\n",
    "        @constraint(m, sum(x[i,:]) + sum(x[:,j])\n",
    "            + sum(x[i+k,j+k] for k = max(1-i,1-j):min(n-i,n-j))\n",
    "            + sum(x[i+k,j-k] for k = max(1-i,n-j):min(n-i,1-j)) >= 1)\n",
    "    end\n",
    "end\n",
    "\n",
    "@objective(m, Min, sum(x))\n",
    "\n",
    "status = solve(m)\n",
    "X = getvalue(x)\n",
    "println(\"A solution is: \")\n",
    "printBoard(X,n)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "A solution is: \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   | X | X |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "| X |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   | X | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   |   |   |   | \n",
      "+---+---+---+---+---+---+---+---+\n",
      "|   |   |   |   |   | X | X |   | \n",
      "+---+---+---+---+---+---+---+---+\n"
     ]
    }
   ],
   "source": [
    "# solve the min queen problem (with symmetry constraint)\n",
    "\n",
    "using JuMP\n",
    "\n",
    "n = 8\n",
    "\n",
    "m = Model()\n",
    "\n",
    "@variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "for i = 1:n\n",
    "    for j = 1:n\n",
    "        @constraint(m, sum(x[i,:]) + sum(x[:,j])\n",
    "            + sum(x[i+k,j+k] for k = max(1-i,1-j):min(n-i,n-j))\n",
    "            + sum(x[i+k,j-k] for k = max(1-i,n-j):min(n-i,1-j)) >= 1)\n",
    "    end\n",
    "end\n",
    "\n",
    "# SYMMETRY CONSTRAINT --- REMOVE THIS FOR NON-SYMMETRIC SOLUTIONS\n",
    "for i in 1:n\n",
    "    for j in i:n\n",
    "        @constraint(m, x[i,j] == x[n-i+1,n-j+1] )\n",
    "    end\n",
    "end\n",
    "\n",
    "@objective(m, Min, sum(x))\n",
    "\n",
    "status = solve(m)\n",
    "X = getvalue(x)\n",
    "println(\"A solution is: \")\n",
    "printBoard(X,n)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.5.0",
   "language": "julia",
   "name": "julia-0.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.5.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
